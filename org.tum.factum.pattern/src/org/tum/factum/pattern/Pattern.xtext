grammar org.tum.factum.pattern.Pattern with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate pattern "http://www.tum.org/factum/pattern/Pattern"

Pattern returns Pattern:
	{Pattern}
	'Pattern' name=ID 'ShortName' psname=ID
	'{'
		//Language Part 1 - Specification of Data Types
		('DTSpec' '{' dtSpec+=DataTypeSpec( "," dtSpec+=DataTypeSpec)* '}' )?
		
		//Language Part 2 - Specification of Component Types (CTypes)
		('CTypes' '{' componentTypes+=ComponentType ( "," componentTypes+=ComponentType)* '}' )?
		
 		//Language Part 3 - Specification of Architectural Constraints
	    ('ArchSpec' '{'
	    	(ctaCmpVar+=ComponentVariable ("," ctaCmpVar+=ComponentVariable)*) 		//Declaration of Component Variables
	    	(ctaDtVars=DTVariables)?												//Declaration of DT Variables

			//(actFormulaIds+=ActivationFormulasID)*

			(ctaFormulaIds+=CtaFormulasID)*
	    	'}'
	    )*
	    //Part 4 - Specification of Architectural Guarantees
	    ('ArchGuarantee' '{'
			(agCmpVar+=ComponentVariable ("," agCmpVar+=ComponentVariable)*)
			(agDtVars=DTVariables)?
			
			(agFormulaIds+=AgFormulasID)*
	    '}')*
	    
		('SubPattern' '{' SubPattern+=Pattern ( "," SubPattern+=Pattern)* '}' )?
	'}'
;
ComponentType returns ComponentType:
	{ComponentType} 
	'CType'	name=ID ('ShortName' ctsname=ID)?
	//Optional package declaration needed for JavaMOP
	('Package' packageName=QualifiedName)?
	'{'
	    //Part 2.1 of Spec
	    ('InputPorts' '{' 
	    	inputPorts+=InputPort ( "," inputPorts+=InputPort)* 
	    	'}'
	    )?
	    //Part 2.2 of Spec
	    ('OutputPorts' '{' 
	    	outputPorts+=OutputPort ( "," outputPorts+=OutputPort)* 
	    	'}'
	    )?
	    //Part 2.3 of Spec
	    ('Parameters' '{' 
	    	parameters+=Parameter ( "," parameters+=Parameter)* 
	    	'}'
	    )?
	    //Part 2.4 of Spec
	    ('Contracts' '{' //Behavior Trace Assertions (BTA)
	    	(btaDtVar+=DataTypeVariable ("," btaDtVar+=DataTypeVariable)*)?
			(behaviorTraceAssertion+=BehaviorTraceAssertion)*
			'}'
	    )?
	    //Part 2.5 of Spec
	   	('Behavior' '{'
	   			('Var' vars+=Variable ( ',' vars+=Variable)*)?
	   			'States' states+=State ( ',' states+=State)*	
	   			'Initial' initial=Initialisation
	   			('Transitions' '{'
					transitions+=Transition
					transitions+=Transition*
					'}'
				)?
	   		'}'
	   	)?   	
	'}'
;

Parameter returns Parameter:
	{Parameter}
	'Parameter' name=ID '(' 
		('Type' ':' paramSrtTyp=[Sort|QualifiedName])? 
	')'
;
OutputPort returns OutputPort:
	{OutputPort} 
	'OutputPort' name=EString '(' 
		('Type' ':' outputPrtSrtTyp=[Sort|QualifiedName])? 
		('connects' connects+=[InputPort|QualifiedName]*)? 
	')'
;
InputPort returns InputPort:
	{InputPort}	
	'InputPort' name=ID '('
		('Type' ':' inputPrtSrtTyp=[Sort|QualifiedName])?
	    ('connects' connects+=[OutputPort|QualifiedName]*)?
	')'
;

// FSM Implementation: BEGIN
// Supported data types by NuXmv
DataType:
	typeName = ('Int' | 'Real' | 'Bool') // NuXmv also supports: Word, Enums, Array, Set
;

Variable returns Variable:
	{Variable}
	name=ID':' dataType=DataType
;

State returns State:
	{State}
	name=ID
;

Initialisation returns Initialisation:
	{Initialisation}
	initialState+=[State|QualifiedName] | 
	'{' 
		'State' initial+=[State|QualifiedName]
		'Var' vars+=[Variable|QualifiedName] '=' (inputPort+=[InputPort|QualifiedName] | number+=Number | boolean+=Bool) 
		(',' vars+=[Variable|QualifiedName] '=' (inputPort+=[InputPort|QualifiedName] | number+=Number | boolean+=Bool))* 
	'}'
;

Transition returns Transition:
	{Transition}
	transitionStart=[State|QualifiedName] 
	('['guards+=FsmFormula']')? '->' ('['guards+=FsmFormula']')?
	transitionEnd=[State|QualifiedName]
;

// FSM Implementation: END

DataTypeSpec returns DataTypeSpec:
	{DataTypeSpec}	
	'DT' name =ID
		//Optional package declaration needed for JavaMOP
		('Package' packageName=QualifiedName)? '('
		('Param' (dtParam+=Sort ("," dtParam+=Sort)*))?
		('Sort' (dtSorts+=Sort ("," dtSorts+=Sort)*))?
		('Operation' (dtOps+=Operation ("," dtOps+=Operation)*))?
		('Predicate' (dtPredicate+=DTPredicate ("," dtPredicate+=DTPredicate)*))?
		(sortName=[Sort|ID] 'generatedBy' (returnOps+=[Operation|ID] ("," returnOps+=[Operation|ID])*))?
	    ('DTA' '{' //Datatype Assertions (DTA)
	    	(dtaDtVar+=DataTypeVariable ("," dtaDtVar+=DataTypeVariable)*)?
			(dataTypeAssertion+=DataTypeAssertion)*
			'}'
	    )?		
	')'
;
DTPredicate:
	name=ID ':' pInput+=[Sort|QualifiedName] ("," pInput+=[Sort|QualifiedName])*
;
Operation:
	name=ID ':'  (dtInput+=[Sort|QualifiedName] ("," dtInput+=[Sort|QualifiedName])*)? '=>' dtOutput=[Sort|QualifiedName]
;
Sort:
	name=ID
;

//Begin common rules for CTA, Architectural Guarantee(AG) and Behavior Trace Assertions (BTA)
VariableStatus: 
	'rig' | 'flex'
;
ComponentVariable:
	//cmptypAssigned => Component Type assigned or associated to the component variable
	VariableStatus	name=ID ':' cmptypAssigned=[ComponentType|ID]?
;
DataTypeVariable:
	VariableStatus name=ID ':' varSortType=[Sort|QualifiedName]
;
DTVariables:
	(dtVars+=DataTypeVariable ("," dtVars+=DataTypeVariable)*)
;
PortRef:
	InputPort | OutputPort | Parameter
;
CmpVariableRef:
	cmpRef=[ComponentVariable|ID] '.' portRef=[PortRef|ID]
;
Term:
	cmpVariableRef=CmpVariableRef |
	dtTypeVars=[DataTypeVariable|ID] | 
	termOperatorFunction=TermOperatorFunction
;
TermOperatorFunction:
	trmOperator=[Operation|QualifiedName] "(" (trmOperands+=Term ("," trmOperands+=Term)*) ")" 
;
RefComponentVariableInputPort:
	inptPrtCmpRef=[ComponentVariable|ID] '.' inputPrtrf=[InputPort|ID]
;
RefComponentVariableOutputPort:
	outptPrtCmpRef=[ComponentVariable|ID] '.' outputPrtrf=[OutputPort|ID]
;
//End

//Begin Configuration Trace Assertions(CTA) - Architectural Configuration or Configuration Trace Assertion 
//BeginBaseTerms
CtaPredicateTerms:
	ctaPTerm1=Term equalityOperator=EqualityElementOperator ctaPTerm2=Term
;
CtaPredicateConn: 
	ctaConn='conn' ('(' ctaConnCmpVarOutputPort=RefComponentVariableOutputPort ','	ctaConnCmpVarInptPort=RefComponentVariableInputPort ')')
; 
CtaPredicateEq: 
	ctaEq='eq' ('(' ctaComponentVariable1=[ComponentVariable|ID] ',' ctaComponentVariable2=[ComponentVariable|ID] ')')
;
CtaPredicateVal:
	ctaVal='val' ('(' valCmpVariableRef=CmpVariableRef ',' ctaValTerms=Term ')') 
;
CtaPredicateCAct:
	cAct='cAct' ('('cActCmpVar=[ComponentVariable|ID] ')')
;
CtaPredicatePAct:
	pAct='pAct' ('(' pActCtaCmpVaref=CmpVariableRef ')') 
;
//EndBaseTerms

//BeginCtaFormulaExpression
CtaFormula returns CtaFormula:
	ctaUnaryFormulas=CtaUnaryFormulas | 
	ctaBinaryFormulas=CtaBinaryFormulas |
	ctaQuantifiedFormulas=CtaQuantifiedFormulas
;
CtaUnaryFormulas returns CtaFormula:
	{CtaUnaryFormulas} (unaryOperator=UnaryOperator ctaFormulaLtl=CtaFormula)
;

CtaQuantifiedFormulas:
 	{CtaQuantifiedFormulas} (quantifierOperator=QuantifierOperator ctaQuantifiedFs=CtaFormula)
;
CtaBinaryFormulas returns CtaFormula:
	{CtaBinaryFormulas}	
	ctaPrimary=CtaPrimary (
		{CtaBinaryFormulas.left=current} binaryOperator=BinaryOperator right=CtaPrimary 
		)*
;
CtaPrimary returns CtaFormula:
	(ctaFormulaWithBracket=CtaFormulaWithBracket | ctaBaseTerms=CtaBaseTerms)
;
CtaFormulaWithBracket:
	leftBracket='(' ctaPrimaryFormula=CtaFormula rightBracket=')'
;
CtaBaseTerms:
		ctaPredicateTerms=CtaPredicateTerms | 
		ctaPredicateConn=CtaPredicateConn | 
		ctaPredicateVal=CtaPredicateVal | 
		ctaPredicateEq=CtaPredicateEq | //This might be duplicate to CtaPredicateTerms?
		ctaPredicateCAct=CtaPredicateCAct | 
		ctaPredicatePAct=CtaPredicatePAct 
;
//EndCtaFormulaExpression

CtaFormulasID:
	name=ID ':' ctaFormula=CtaFormula
;
// End CTA

// Begin Architectural Guarantee
//BeginBaseTerms
AgPredicateTerms:
	agPTerm1=Term equalityOperator=EqualityElementOperator agPTerm2=Term
;
AgPredicateConn: 
	agConn='conn' ('(' agConnCmpVarOutputPort=RefComponentVariableOutputPort ',' agConnCmpVarInptPort=RefComponentVariableInputPort ')')
; 
AgPredicateEq: 
	agEq='eq' ('(' agComponentVariable1=[ComponentVariable|ID] ',' agComponentVariable2=[ComponentVariable|ID] ')')
;
AgPredicateVal:
	agVal='val' ('(' valCmpVariableRef=CmpVariableRef ',' agValTerms=Term ')') 
;
AgPredicateCAct:
	cAct='cAct' ('('cActCmpVar=[ComponentVariable|ID] ')')
;
AgPredicatePAct:
	pAct='pAct' ('(' pActAgCmpVaref=CmpVariableRef ')') 
;
//EndBaseTerms

//BeginAgFormulaExpression
AgFormula returns AgFormula:
	agUnaryFormulas=AgUnaryFormulas | 
	agBinaryFormulas=AgBinaryFormulas |
	agQuantifiedFormulas=AgQuantifiedFormulas
;
AgUnaryFormulas returns AgFormula:
	{AgUnaryFormulas} (unaryOperator=UnaryOperator agFormulaLtl=AgFormula)
;
AgQuantifiedFormulas:
 	{AgQuantifiedFormulas} (quantifierOperator=QuantifierOperator agQuantifiedFs=AgFormula)
;
AgBinaryFormulas returns AgFormula:
	{AgBinaryFormulas}	
	agPrimary=AgPrimary (
		{AgBinaryFormulas.left=current} binaryOperator=BinaryOperator right=AgPrimary 
		)*
;
AgPrimary returns AgFormula:
	(agFormulaWithBracket=AgFormulaWithBracket | agBaseTerms=AgBaseTerms)
;
AgFormulaWithBracket:
	leftBracket='(' agPrimaryFormula=AgFormula rightBracket=')'
;
AgBaseTerms:
		agPredicateTerms=AgPredicateTerms | 
		agPredicateConn=AgPredicateConn | 
		agPredicateVal=AgPredicateVal | 
		agPredicateEq=AgPredicateEq |
		agPredicateCAct=AgPredicateCAct | 
		agPredicatePAct=AgPredicatePAct 
;
//EndAgFormulaExpression
AgFormulasID:
	name=ID ':' agFormula=AgFormula
;
// End AG

// Datatype Assertions: BEGIN
DataTypeAssertion:
	name=ID ':' dtaFormula=DtaFormula
;
DtaFormula:
	DtaQuantification |
	DtaLImp
;
DtaQuantification returns DtaFormula:
	QuantifierOperator dtaFormula=DtaLImp
;
DtaLImp returns DtaFormula:
	DtaLOr ({DtaLImp.left=current} ('⇒'|'==>') right=DtaLOr)*
;
DtaLOr returns DtaFormula:
	DtaLAnd ({DtaLOr.left=current} ('∨'|'\\/') right=DtaLAnd)*
;
DtaLAnd returns DtaFormula:
	DtaPrimary ({DtaLAnd.left=current} ('∧'|'/\\') right=DtaPrimary)*
;
DtaPrimary returns DtaFormula:
	{Neg} ('¬'|'~') dtaFormula=DtaPrimary |
	'(' DtaFormula ')' |
	DtaBase
;
DtaBase returns DtaFormula:
	{DtaPredicate} dtaPredicate=[DTPredicate|QualifiedName] '[' params=DtaOpParam? ']' |
	{DtaTermEq} '[' left=DtaTerm '=' right=DtaTerm ']'
;
DtaTerm:
	{DtaVariable} var=[DataTypeVariable|ID] |
	{DtaOperation} dtaTrmOperator=[Operation|QualifiedName] '[' params=DtaOpParam? ']'
;
DtaOpParam:
	dtaOperands+=DtaTerm ("," dtaOperands+=DtaTerm)*
;
// Datatype Assertions: END

// Behavior Trace Assertions: BEGIN
BehaviorTraceAssertion:
	name=ID ':' btaFormula=BtaFormula
;
BtaFormula:
	BtaQuantification |
	BtaLImp
;
BtaQuantification returns BtaFormula:
	QuantifierOperator btaFormula=BtaWUntil |
	LTLOperators btaFormula=BtaLImp
;
BtaWUntil returns BtaFormula:
	BtaSUntil ({BtaWUntil.left=current} 'W' right=BtaSUntil)*
;
BtaSUntil returns BtaFormula:
	BtaLImp ({BtaSUntil.left=current} 'U' right=BtaLImp)*
;
BtaLImp returns BtaFormula:
	BtaLOr ({BtaLImp.left=current} ('⇒'|'==>') right=BtaLOr)*
;
BtaLOr returns BtaFormula:
	BtaLAnd ({BtaLOr.left=current} ('∨'|'\\/') right=BtaLAnd)*
;
BtaLAnd returns BtaFormula:
	BtaPrimary ({BtaLAnd.left=current} ('∧'|'/\\') right=BtaPrimary)*
;
BtaPrimary returns BtaFormula:
	{Neg} ('¬'|'~') btaFormula=BtaPrimary |
	'(' BtaFormula ')' |
	BtaBase
;
BtaBase returns BtaFormula:
	{BtaPredicate} btaPredicate=[DTPredicate|QualifiedName] '[' params=BtaOpParam? ']' |
	{BtaTermEq} '[' left=BtaTerm '=' right=BtaTerm ']'
;
BtaTerm:
	{BtaVariable} var=[DataTypeVariable|ID] |
	prtRef=PortRef |
	{BtaOperation} btaTrmOperator=[Operation|QualifiedName] '[' params=BtaOpParam? ']'
;
BtaOpParam:
	btaOperands+=BtaTerm ("," btaOperands+=BtaTerm)*
;
// Behavior Trace Assertions: END 

// FSM Assertions: BEGIN
FsmFormula:
	FsmLImp 
;
FsmLImp returns FsmFormula:
	FsmLOr ({FsmLImp.left=current} ('⇒'|'==>') right=FsmLOr)*
;
FsmLOr returns FsmFormula:
	FsmLAnd ({FsmLOr.left=current} ('∨'|'\\/') right=FsmLAnd)*
;
FsmLAnd returns FsmFormula:
	FsmEquality ({FsmLAnd.left=current} ('∧'|'/\\') right=FsmEquality)*
;
FsmEquality returns FsmFormula:
	 FsmComparison ({FsmEquality.left=current} ("=="|"!=") right=FsmComparison)*
;
FsmComparison returns FsmFormula: 
	FsmPlusMinus ({Comparison.left=current} (">="|"<="|">"|"<") right=FsmPlusMinus)*
;
FsmPlusMinus returns FsmFormula:
	FsmMultDiv (({Plus.left=current} '+' | {Minus.left=current} '-' ) right=FsmMultDiv)*
;
FsmMultDiv returns FsmFormula:
	FsmEquals (({Mult.left=current} '*'  | {Div.left=current} '/' ) right=FsmEquals)*
;
FsmEquals returns FsmFormula:
	 FsmPrimary ({Equals.left=current} '=' rigth=FsmPrimary)*
;
FsmPrimary returns FsmFormula:
	{LogicNeg} ('¬'|'~') fsmPrimary=FsmPrimary |
	{MathNeg} ('-') fsmPrimary=FsmPrimary |
	'(' FsmFormula ')' |
	FsmBase
;
FsmBase returns FsmFormula:
	number=Number |
	bool=Bool |
	var=[Variable|ID]
;
Number:
	{Int} INT | 
	{Real} INT'.'INT 
;
Bool:
	'True' | 'False'
;
// FSM Assertions: END

//Begin Operator rules for CTA, BTA, and AG ....
UnaryLogicOperator:
	neg='¬' // Negation logic operator
;
BinaryLogicOperator:
	lAnd='∧' | 
	lDisjunct='∨' |
	lImplies='⇒' | lDoubleImplies='⇔'
;
//Temporal Operators
LTLOperators:
	ltlG='G' | ltlX='X' |	ltlF='F'
;
UnaryOperator:
	ltlG='G' | ltlX='X' |	ltlF='F' | //Linear Temporal Logic Operators
	neg='¬' // Negation logic operator
;
BinaryOperator: //operates F and F, F is a formula (CtaFormula,  AgFormula, and BtaFormula)
	lAnd='∧' | 
	lDisjunct='∨' |
	lImplies='⇒' | lDoubleImplies='⇔' |
	lWeakUntil='W' | lUntil='U'
;
/*
BinaryOperatorOr: //operates F and F, F is a formula (CtaFormula,  AgFormula, and BtaFormula)
	lDisjunct='∨'
;
BinaryOperatorAnd:
	lAnd='∧'
;
BinaryOperatorImplies:
	lImplies='⇒' | lDoubleImplies='⇔'
;
BinaryOperatorUntil:
	lWeakUntil='W' | lUntil='U'
;
*/
EqualityElementOperator:
	'='
;
QuantifierOperatorDT:
	exists='∃'quantifiedExistsVar=[DataTypeVariable|ID]'.' | all='∀'quantifiedAllVar=[DataTypeVariable|ID]'.'
;
QuantifierOperator:
	exists='∃'quantifiedExistsVar=[ComponentVariable|ID]'.' | all='∀'quantifiedAllVar=[ComponentVariable|ID]'.'
;
//End
QualifiedName:
    ID ('.' ID)*
;
EString returns ecore::EString:
	STRING | ID
;